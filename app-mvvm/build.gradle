apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        applicationId "uk.ivanc.archimvvm"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    Map<String, String> dependencies = rootProject.ext.dependencies;

    compile dependencies.appCompat
    compile dependencies.cardView
    compile dependencies.recyclerView
    compile dependencies.retrofit
    compile dependencies.retrofitConverterGson
    compile dependencies.retrofitAdapterRxJava
    compile dependencies.picasso
    compile dependencies.rxAndroid
    compile dependencies.circleImageView

    provided 'javax.annotation:javax.annotation-api:1.2'
    provided 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'de.greenrobot:eventbus:2.4.0'

    testCompile dependencies.jUnit
    testCompile dependencies.mockito
    testCompile dependencies.robolectric
}

// fix for dataBinder: Source folders generated at incorrect location
android.applicationVariants.all { variant ->
    def variantName = variant.name.capitalize()
    def inputDir = "${buildDir}/intermediates/classes/${variant.dirName}"
    def sourceDir = "${buildDir}/generated/source/dataBinding/${variant.dirName}"
    def copyTask = tasks.create(name: "dataBindingFix${variantName}", type: Copy) {
        from inputDir
        into sourceDir
        include '**/*.java'
    }
    tasks["generate${variantName}Sources"].dependsOn copyTask
    variant.addJavaSourceFoldersToModel new File(sourceDir)
}
